rule gauss_run2:
  output:
    sample = SAMPLES + '/{eventtype}/{year}/{magnet}/{flag}_G{gaussversion}/{bunch}.sim',
  log:
    job = SAMPLES + '/{eventtype}/{year}/{magnet}/{flag}_G{gaussversion}/{bunch}.py',
    runner = SAMPLES + '/{eventtype}/{year}/{magnet}/{flag}_G{gaussversion}/{bunch}.sh',
    sandbox = directory(SAMPLES + '/{eventtype}/{year}/{magnet}/{flag}_G{gaussversion}/{bunch}_sandbox'),
    log = SAMPLES + '/{eventtype}/{year}/{magnet}/{flag}_G{gaussversion}/{bunch}.log'
  run:
    # create job file for gaudirun from template
    t = Template(open('gaudi/gauss/job.py').read())
    with open(f"{log.job}", "w") as job:
      job.write(t.substitute(dict(
          sim_file=f'{output.sample}',
          number_of_events=f"{nevts}")))
    # create bash file to run Gauss from template
    t = Template(open('gaudi/gauss/runner.sh').read())
    with open(f"{log.runner}", "w") as job:
      job.write(t.substitute(dict(
          GAUSS_VERSION=f'{wildcards.gaussversion}',
          EVENT_TYPE=f"{wildcards.eventtype}",
          YEAR=f"{wildcards.year}",
          JOB_FILE=f"{log.job}",
          APPCONFIGOPTS=f"{APPCONFIGOPTS}",
          DECFILESROOT=f"{DECFILESROOT}",
          SANDBOX=f"{log.sandbox}",
          MC_PROD=f"{MC_PROD}"
      )))
    # run simulation
    shell(f"(time env -i bash --noprofile --norc {log.runner}) &> {log.log}")
